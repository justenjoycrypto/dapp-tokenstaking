// SPDX-License-Identifier: GPL-3.0

pragma solidity ^0.8.4;

import "@openzeppelin/contracts/utils/math/Math.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";

error InvalidStartingTimestamp();
error InvalidDuration();
error InconsistentRewardTokensAndAmounts();
error ZeroAddressRewardToken();
error ZeroRewardAmount();
error NotEnoughFunding();
error ZeroAddressStakableToken();
error AlreadyStarted();
error ZeroStakeAmount();
error StakingCapSurpassed();
error ZeroWithdrawAmount();
error FundsLocked();
error InsufficientStake();
error InconsistentClaimedAmounts();
error InsufficientClaimableAmount();
error ZeroAddressOwner();
error CallerNotOwner();
error AlreadyInitialized();
error InvalidCancelationState();
error NotStarted();
error AlreadyEnded();
error NoRecoverableRewards();
error NoClaimableRewards();

contract StandaloneERC20StakingRewardsDistribution {
    using SafeERC20 for IERC20;

    uint224 constant MULTIPLIER = 2**112;

    struct Reward {
        address token;
        uint256 amount;
        uint256 perStakedToken;
        uint256 recoverableSeconds;
        uint256 claimed;
    }

    struct StakerRewardInfo {
        uint256 consolidatedPerStakedToken;
        uint256 earned;
        uint256 claimed;
    }

    struct Staker {
        uint256 stake;
        mapping(address => StakerRewardInfo) rewardInfo;
    }

    Reward[] public rewards;
    mapping(address => Staker) public stakers;
    uint64 public startingTimestamp;
    uint64 public endingTimestamp;
    uint64 public secondsDuration;
    uint64 public lastConsolidationTimestamp;
    IERC20 public stakableToken;
    address public owner;
    bool public locked;
    bool public canceled;
    bool public initialized;
    uint256 public totalStakedTokensAmount;
    uint256 public stakingCap;

    event OwnershipTransferred(
        address indexed previousOwner,
        address indexed newOwner
    );
    event Initialized(
        address[] rewardsTokenAddresses,
        address stakableTokenAddress,
        uint256[] rewardsAmounts,
        uint64 startingTimestamp,
        uint64 endingTimestamp,
        bool locked,
        uint256 stakingCap
    );
    event Canceled();
    event Staked(address indexed staker, uint256 amount);
    event Withdrawn(address indexed withdrawer, uint256 amount);
    event Claimed(address indexed claimer, uint256[] amounts);
    event Recovered(uint256[] amounts);

    function initialize(
        address[] calldata _rewardTokenAddresses,
        address _stakableTokenAddress,
        uint256[] calldata _rewardAmounts,
        uint64 _startingTimestamp,
        uint64 _endingTimestamp,
        bool _locked,
        uint256 _stakingCap
    ) external {
        if(initialized) revert AlreadyInitialized();
        if(_startingTimestamp <= block.timestamp) revert InvalidStartingTimestamp();
        if(_endingTimestamp <= _startingTimestamp) revert InvalidDuration();
        if(_rewardTokenAddresses.length != _rewardAmounts.length) revert InconsistentRewardTokensAndAmounts();

        secondsDuration = _endingTimestamp - _startingTimestamp;
        // Initializing reward tokens and amounts
        for (uint32 _i = 0; _i < _rewardTokenAddresses.length; _i++) {
            address _rewardTokenAddress = _rewardTokenAddresses[_i];
            uint256 _rewardAmount = _rewardAmounts[_i];
            if(_rewardTokenAddress == address(0)) revert ZeroAddressRewardToken();
            if(_rewardAmount == 0) revert ZeroRewardAmount();
            IERC20(_rewardTokenAddress).safeTransferFrom(msg.sender, address(this), _rewardAmount);
            rewards.push(
                Reward({
                    token: _rewardTokenAddress,
                    amount: _rewardAmount,
                    perStakedToken: 0,
                    recoverableSeconds: 0,
                    claimed: 0
                })
            );
        }

        if(_stakableTokenAddress == address(0)) revert ZeroAddressStakableToken();
        stakableToken = IERC20(_stakableTokenAddress);

        owner = msg.sender;
        startingTimestamp = _startingTimestamp;
        endingTimestamp = _endingTimestamp;
        lastConsolidationTimestamp = _startingTimestamp;
        locked = _locked;
        stakingCap = _stakingCap;
        initialized = true;
        canceled = false;

        emit Initialized(
            _rewardTokenAddresses,
            _stakableTokenAddress,
            _rewardAmounts,
            _startingTimestamp,
            _endingTimestamp,
            _locked,
            _stakingCap
        );
    }

    function cancel() external {
        if(owner != msg.sender) revert CallerNotOwner();
        if(!initialized || canceled || block.timestamp >= startingTimestamp) revert InvalidCancelationState();
        for (uint256 _i; _i < rewards.length; _i++) {
            Reward storage _reward = rewards[_i];
            IERC20(_reward.token).safeTransfer(
                owner,
                IERC20(_reward.token).balanceOf(address(this))
            );
        }
        canceled = true;
        emit Canceled();
    }

    function recoverUnassignedRewards() external {
        if(
            !initialized || canceled || block.timestamp < startingTimestamp
        ) revert NotStarted();
        consolidateReward();
        uint256[] memory _recoveredUnassignedRewards =
            new uint256[](rewards.length);
        bool _atLeastOneNonZeroRecovery = false;
        for (uint256 _i; _i < rewards.length; _i++) {
            Reward storage _reward = rewards[_i];
            // recoverable rewards are going to be recovered in this tx (if it does not revert),
            // so we add them to the claimed rewards right now
            _reward.claimed += ((_reward.recoverableSeconds * _reward.amount) /
                (uint256(secondsDuration) * MULTIPLIER));
            delete _reward.recoverableSeconds;
            uint256 _recoverableRewards =
                IERC20(_reward.token).balanceOf(address(this)) -
                    (_reward.amount - _reward.claimed);
            if (!_atLeastOneNonZeroRecovery && _recoverableRewards > 0)
                _atLeastOneNonZeroRecovery = true;
            _recoveredUnassignedRewards[_i] = _recoverableRewards;
            IERC20(_reward.token).safeTransfer(owner, _recoverableRewards);
        }
        if(!_atLeastOneNonZeroRecovery) revert NoRecoverableRewards();
        emit Recovered(_recoveredUnassignedRewards);
    }

    function stake(uint256 _amount) external {
        if(
            !initialized ||
                canceled ||
                block.timestamp < startingTimestamp ||
                block.timestamp >= endingTimestamp
        ) revert AlreadyEnded();
        if(_amount == 0) revert ZeroStakeAmount();
        if (stakingCap > 0 && totalStakedTokensAmount + _amount > stakingCap) revert StakingCapSurpassed();
        consolidateReward();
        Staker storage _staker = stakers[msg.sender];
        _staker.stake += _amount;
        totalStakedTokensAmount += _amount;
        stakableToken.safeTransferFrom(msg.sender, address(this), _amount);
        emit Staked(msg.sender, _amount);
    }

    function withdraw(uint256 _amount) public {
        if(
            !initialized || canceled || block.timestamp < startingTimestamp
        ) revert NotStarted();
        if(_amount == 0) revert ZeroWithdrawAmount();
        if (locked && block.timestamp <= endingTimestamp) revert FundsLocked();
        consolidateReward();
        Staker storage _staker = stakers[msg.sender];
        if(_staker.stake < _amount) revert InsufficientStake();
        _staker.stake -= _amount;
        totalStakedTokensAmount -= _amount;
        stakableToken.safeTransfer(msg.sender, _amount);
        emit Withdrawn(msg.sender, _amount);
    }

    function claim(uint256[] memory _amounts, address _recipient) external {
        if(
            !initialized || canceled || block.timestamp < startingTimestamp
            
        ) revert NotStarted();
        if(_amounts.length != rewards.length) revert InconsistentClaimedAmounts();
        consolidateReward();
        Staker storage _staker = stakers[msg.sender];
        uint256[] memory _claimedRewards = new uint256[](rewards.length);
        bool _atLeastOneNonZeroClaim = false;
        for (uint256 _i; _i < rewards.length; _i++) {
            Reward storage _reward = rewards[_i];
            StakerRewardInfo storage _stakerRewardInfo =
                _staker.rewardInfo[_reward.token];
            uint256 _claimableReward =
                _stakerRewardInfo.earned - _stakerRewardInfo.claimed;
            uint256 _wantedAmount = _amounts[_i];
            if(_claimableReward < _wantedAmount) revert InsufficientClaimableAmount();
            if (!_atLeastOneNonZeroClaim && _wantedAmount > 0)
                _atLeastOneNonZeroClaim = true;
            if(_wantedAmount > 0) {
                _stakerRewardInfo.claimed += _wantedAmount;
                _reward.claimed += _wantedAmount;
                IERC20(_reward.token).safeTransfer(_recipient, _wantedAmount);
            }
            _claimedRewards[_i] = _wantedAmount;
        }
        if(!_atLeastOneNonZeroClaim) revert NoClaimableRewards();
        emit Claimed(msg.sender, _claimedRewards);
    }

    function claimAll(address _recipient) public {
        if(
            !initialized || canceled || block.timestamp < startingTimestamp
        ) revert NotStarted();
        consolidateReward();
        Staker storage _staker = stakers[msg.sender];
        uint256[] memory _claimedRewards = new uint256[](rewards.length);
        bool _atLeastOneNonZeroClaim = false;
        for (uint256 _i; _i < rewards.length; _i++) {
            Reward storage _reward = rewards[_i];
            StakerRewardInfo storage _stakerRewardInfo =
                _staker.rewardInfo[_reward.token];
            uint256 _claimableReward =
                _stakerRewardInfo.earned - _stakerRewardInfo.claimed;
            if (!_atLeastOneNonZeroClaim && _claimableReward > 0)
                _atLeastOneNonZeroClaim = true;
            if(_claimableReward > 0) {
                _stakerRewardInfo.claimed += _claimableReward;
                _reward.claimed += _claimableReward;
                IERC20(_reward.token).safeTransfer(_recipient, _claimableReward);
            }
            _claimedRewards[_i] = _claimableReward;
        }
        if(!_atLeastOneNonZeroClaim) revert NoClaimableRewards();
        emit Claimed(msg.sender, _claimedRewards);
    }

    function exit(address _recipient) external {
        if(
            !initialized || canceled || block.timestamp < startingTimestamp
        ) revert NotStarted();
        claimAll(_recipient);
        withdraw(stakers[msg.sender].stake);
    }

    function consolidateReward() private {
        uint64 _consolidationTimestamp =
            uint64(Math.min(block.timestamp, endingTimestamp));
        uint256 _lastPeriodDuration =
            uint256(_consolidationTimestamp - lastConsolidationTimestamp);
        Staker storage _staker = stakers[msg.sender];
        for (uint256 _i; _i < rewards.length; _i++) {
            Reward storage _reward = rewards[_i];
            StakerRewardInfo storage _stakerRewardInfo =
                _staker.rewardInfo[_reward.token];
            if (_lastPeriodDuration > 0) {
                if (totalStakedTokensAmount == 0) {
                    _reward.recoverableSeconds +=
                        _lastPeriodDuration *
                        MULTIPLIER;
                    // no need to update the reward per staked token since in this period
                    // there have been no staked tokens, so no reward has been given out to stakers
                } else {
                    _reward.perStakedToken += ((_lastPeriodDuration *
                        _reward.amount *
                        MULTIPLIER) /
                        (totalStakedTokensAmount * secondsDuration));
                }
            }
            uint256 _rewardSinceLastConsolidation =
                (_staker.stake *
                    (_reward.perStakedToken -
                        _stakerRewardInfo.consolidatedPerStakedToken)) /
                    MULTIPLIER;
            if (_rewardSinceLastConsolidation > 0) {
                _stakerRewardInfo.earned += _rewardSinceLastConsolidation;
            }
            _stakerRewardInfo.consolidatedPerStakedToken = _reward
                .perStakedToken;
        }
        lastConsolidationTimestamp = _consolidationTimestamp;
    }

    function claimableRewards(address _account)
        public
        view
        returns (uint256[] memory)
    {
        uint256[] memory _outstandingRewards = new uint256[](rewards.length);
        if (!initialized || block.timestamp < startingTimestamp) {
            for (uint256 _i; _i < rewards.length; _i++) {
                _outstandingRewards[_i] = 0;
            }
            return _outstandingRewards;
        }
        Staker storage _staker = stakers[_account];
        uint64 _consolidationTimestamp =
            uint64(Math.min(block.timestamp, endingTimestamp));
        uint256 _lastPeriodDuration =
            uint256(_consolidationTimestamp - lastConsolidationTimestamp);
        for (uint256 _i; _i < rewards.length; _i++) {
            Reward storage _reward = rewards[_i];
            StakerRewardInfo storage _stakerRewardInfo =
                _staker.rewardInfo[_reward.token];
            uint256 _localRewardPerStakedToken = _reward.perStakedToken;
            if (_lastPeriodDuration > 0 && totalStakedTokensAmount > 0) {
                _localRewardPerStakedToken += ((_lastPeriodDuration *
                    _reward.amount *
                    MULTIPLIER) / (totalStakedTokensAmount * secondsDuration));
            }
            uint256 _rewardSinceLastConsolidation =
                (_staker.stake *
                    (_localRewardPerStakedToken -
                        _stakerRewardInfo.consolidatedPerStakedToken)) /
                    MULTIPLIER;
            _outstandingRewards[_i] =
                _rewardSinceLastConsolidation +
                (_stakerRewardInfo.earned - _stakerRewardInfo.claimed);
        }
        return _outstandingRewards;
    }

    function getRewardTokens() external view returns (address[] memory) {
        address[] memory _rewardTokens = new address[](rewards.length);
        for (uint256 _i = 0; _i < rewards.length; _i++) {
            _rewardTokens[_i] = rewards[_i].token;
        }
        return _rewardTokens;
    }

    function rewardAmount(address _rewardToken)
        external
        view
        returns (uint256)
    {
        for (uint256 _i = 0; _i < rewards.length; _i++) {
            Reward storage _reward = rewards[_i];
            if (_rewardToken == _reward.token) return _reward.amount;
        }
        return 0;
    }

    function stakedTokensOf(address _staker) external view returns (uint256) {
        return stakers[_staker].stake;
    }

    function earnedRewardsOf(address _staker)
        external
        view
        returns (uint256[] memory)
    {
        Staker storage _stakerFromStorage = stakers[_staker];
        uint256[] memory _earnedRewards = new uint256[](rewards.length);
        for (uint256 _i; _i < rewards.length; _i++) {
            _earnedRewards[_i] = _stakerFromStorage.rewardInfo[
                rewards[_i].token
            ]
                .earned;
        }
        return _earnedRewards;
    }

    function recoverableUnassignedReward(address _rewardToken)
        external
        view
        returns (uint256)
    {
        for (uint256 _i = 0; _i < rewards.length; _i++) {
            Reward storage _reward = rewards[_i];
            if (_reward.token == _rewardToken) {
                uint256 _nonRequiredFunds =
                    _reward.claimed +
                        ((_reward.recoverableSeconds * _reward.amount) /
                            (uint256(secondsDuration) * MULTIPLIER));
                return
                    IERC20(_reward.token).balanceOf(address(this)) -
                    (_reward.amount - _nonRequiredFunds);
            }
        }
        return 0;
    }

    function getClaimedRewards(address _claimer)
        external
        view
        returns (uint256[] memory)
    {
        Staker storage _staker = stakers[_claimer];
        uint256[] memory _claimedRewards = new uint256[](rewards.length);
        for (uint256 _i = 0; _i < rewards.length; _i++) {
            Reward storage _reward = rewards[_i];
            _claimedRewards[_i] = _staker.rewardInfo[_reward.token].claimed;
        }
        return _claimedRewards;
    }

    function renounceOwnership() public {
        if(owner != msg.sender) revert CallerNotOwner();
        owner = address(0);
        emit OwnershipTransferred(owner, address(0));
    }

    function transferOwnership(address _newOwner) public {
        if(owner != msg.sender) revert CallerNotOwner();
        if(_newOwner == address(0)) revert ZeroAddressOwner();
        emit OwnershipTransferred(owner, _newOwner);
        owner = _newOwner;
    }
}
